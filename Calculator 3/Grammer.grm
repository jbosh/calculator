"Name"     = Calculator

"Start Symbol" = <Statement>
"Auto Whitespace" = True

! ------------------------------------------------- Sets


! ------------------------------------------------- Terminals
![{Letter}|{Printable Extended}][{Digit}{Letter}|{Printable Extended}]*
Id = ({Letter}|{Printable Extended})({Digit}|{Letter}|{Printable Extended})*
Double = {Digit}+ '.' {Digit}*
    | '.' {Digit}+  
    | {Digit}+

    | {Digit}+ 'E' {Digit}+
    | {Digit}+ 'E' '.' {Digit}+
    | {Digit}+ 'E' {Digit}+ '.' {Digit}*

    | '.' {Digit}+ 'E' {Digit}+
    | '.' {Digit}+ 'E' '.' {Digit}+
    | '.' {Digit}+ 'E' {Digit}+ '.' {Digit}*

    | {Digit}+ '.' {Digit}* 'E' {Digit}+
    | {Digit}+ '.' {Digit}* 'E' '.' {Digit}+
    | {Digit}+ '.' {Digit}* 'E' {Digit}+ '.' {Digit}*

! ------------------------------------------------- Rules
<Statement> ::= <Expression>
              | Id '=' <Expression>

<Expression> ::= <Expression> '+' <Op Exp> 
               | <Expression> '-' <Op Exp> 
               | <Op Exp> 

<Op Exp> ::= <Op Exp> '*' <Pow Exp> 
           | <Op Exp> '/' <Pow Exp>
           | <Op Exp> '%' <Pow Exp>
           | <Pow Exp>

<Pow Exp> ::= <Pow Exp> '^' <Negate Exp>
            | <Negate Exp>

<Negate Exp>  ::= '~' <Negate Exp> 
                | <Factorial>

<Factorial> ::= <Factorial> '!'
              | <Function>

<Function> ::= 'sin' <Function>
             | 'cos' <Function>
             | 'tan' <Function>
             | 'atan' <Function>
             | 'acos' <Function>
             | 'asin' <Function>
             | 'abs' <Function>
             | 'deg' <Function>
             | 'rad' <Function>
             | 'ln' <Function>
             | 'log' <Function>
             | 'sqrt' <Function>
             | <Value>

<Value>       ::= Double
                | ID
                | '(' <Expression> ')'
