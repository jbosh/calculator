"Name"     = Calculator

"Start Symbol" = <Statement>
"Auto Whitespace" = True

! ------------------------------------------------- Sets


! ------------------------------------------------- Terminals
![{Letter}|{Printable Extended}][{Digit}{Letter}|{Printable Extended}]*
!{ReadableUnicode} = {&0100 .. &D7FF} + {&DC00 .. &FFF0}
{ReadableUnicode} = {&0200 .. &0400} + {&2603}
Id = ({Letter}|{Printable Extended}|{ReadableUnicode})({Digit}|{Letter}|{Printable Extended}|{ReadableUnicode})*
Double = {Digit}+ '.' {Digit}*
    | '.' {Digit}+  
    | {Digit}+

    | {Digit}+ 'E' {Digit}+
    | {Digit}+ 'E' '.' {Digit}+
    | {Digit}+ 'E' {Digit}+ '.' {Digit}*

    | '.' {Digit}+ 'E' {Digit}+
    | '.' {Digit}+ 'E' '.' {Digit}+
    | '.' {Digit}+ 'E' {Digit}+ '.' {Digit}*

    | {Digit}+ '.' {Digit}* 'E' {Digit}+
    | {Digit}+ '.' {Digit}* 'E' '.' {Digit}+
    | {Digit}+ '.' {Digit}* 'E' {Digit}+ '.' {Digit}*

    | {Digit}+ 'E' '-' {Digit}+
    | {Digit}+ 'E' '-' '.' {Digit}+
    | {Digit}+ 'E' '-' {Digit}+ '.' {Digit}*

    | '.' {Digit}+ 'E' '-' {Digit}+
    | '.' {Digit}+ 'E' '-' '.' {Digit}+
    | '.' {Digit}+ 'E' '-' {Digit}+ '.' {Digit}*

    | {Digit}+ '.' {Digit}* 'E' '-' {Digit}+
    | {Digit}+ '.' {Digit}* 'E' '-' '.' {Digit}+
    | {Digit}+ '.' {Digit}* 'E' '-' {Digit}+ '.' {Digit}*
Hex = '0x'[ABCDEFabcdef0123456789]+
Binary = '0b'[10]+

! ------------------------------------------------- Rules
<Statement> ::= <Logical Expression>
              | Id '=' <Logical Expression>

<Logical Expression> ::= <Logical Expression> '&' <Shift Expression>
                       | <Logical Expression> '|' <Shift Expression>
                       | <Shift Expression>

<Shift Expression> ::= <Shift Expression> '<<' <Expression> 
                     | <Shift Expression> '>>' <Expression>
                     | <Expression>

<Expression> ::= <Expression> '+' <Op Exp> 
               | <Expression> '-' <Op Exp> 
               | <Op Exp> 

<Op Exp> ::= <Op Exp> '*' <Pow Exp> 
           | <Op Exp> '/' <Pow Exp>
           | <Op Exp> '%' <Pow Exp>
           | <Pow Exp>

<Pow Exp> ::= <Pow Exp> '^' <Negate Exp>
            | <Negate Exp>

<Negate Exp>  ::= '~' <Negate Exp>
                | '-' <Negate Exp> 
                | <Factorial>

<Factorial> ::= <Factorial> '!'
              | <Function>

<Function> ::= 'sin' <Function>
             | 'cos' <Function>
             | 'tan' <Function>
             | 'atan' <Function>
             | 'acos' <Function>
             | 'asin' <Function>
             | 'abs' <Function>
             | 'deg' <Function>
             | 'rad' <Function>
             | 'ln' <Function>
             | 'log' <Function>
             | 'sqrt' <Function>
             | 'cross' <Vector Expression>
             | 'dot' <Vector Expression>
             | 'normalize' <Function>
             | 'length' <Function>
             | 'round' <Function>
             | 'floor' <Function>
             | 'ceiling' <Function>
             | 'endian' <Function>
             | <Value>

<Value> ::= Double
          | ID
          | '(' <Logical Expression> ')'
          | Hex
          | Binary
          | <Vector Expression>

<Vector Expression> ::= '{' <Expression List> '}'

<Expression List> ::= <Expression List> ';' <Logical Expression>
                    | <Logical Expression>


